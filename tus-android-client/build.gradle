apply plugin: 'com.android.library'
apply plugin: 'maven'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.github.LeonDevLifeLog:tus-java-client:v1.0.1'
    testImplementation 'org.robolectric:robolectric:2.4'
    testImplementation 'junit:junit:4.12'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath = configurations.compile
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task createPom {
    pom {
        project {
            groupId 'io.tus.android.client'
            artifactId 'tus-android-client'
            name 'tus-android-client'
            version project.getProperties().get('pomVersion')
            packaging 'aar'
            description 'Android client for tus, the resumable file uploading protocol.'
            url 'http://tus.io'

            scm {
                url 'https://github.com/tus/tus-android-client'
                connection 'https://github.com/tus/tus-android-client'
                developerConnection 'https://github.com/tus/tus-android-client'
            }
            
            developers {
                developer {
                    id 'acconut'
                    name 'Marius Kleidl'
                    email 'marius@transloadit.com'
                }
            }

            inceptionYear '2015'
            licenses {
                license {
                    name 'The MIT License (MIT)'
                    url 'http://opensource.org/licenses/MIT'
                }
            }
        }
    }.writeTo("build/libs/pom.xml")
}
